plugins {
    id 'org.springframework.boot' version '3.1.8' apply false
    id 'io.spring.dependency-management' version '1.1.4' apply false
    id 'com.google.cloud.tools.jib' version '3.4.2' apply false
}


group = 'org.coreservices'
version = '1.0-SNAPSHOT'
ext {
    set('springCloudVersion', "2022.0.5")
}

allprojects {

    repositories {
        google()
        mavenLocal()
        mavenCentral()
    }

    apply plugin: 'java'
    apply plugin: 'idea'

    sourceCompatibility = '17'
    dependencies {}

}

Boolean isSpringBootProject(String name) {
    String projectRoot = rootProject.projectDir
    List projectRoots = [dependencySource, servicesRootPath]
    for (String projectRootName : projectRoots) {
        File rootPath = new File(projectRoot, projectRootName)
        List applications = ["applications"]
        for (String applicationName : applications) {
            File subsectionDirectory = new File(rootPath, applicationName)
            if (subsectionDirectory.exists()) {
                if (subsectionDirectory.list().contains(name)) {
                    return true
                }

            }
        }
    }
    return false
}


subprojects { project ->
    // Apply common dependencies to all projects except the server-location project
    if (isSpringBootProject(project.name)) {
        println("Loading Applications: " + project.name)
        apply from: "${rootProject.projectDir}/${commonDependencies}/plugins.gradle"
        if (project.name != "server-location") {
            apply from: "${rootProject.projectDir}/${commonDependencies}/dependencies.gradle"
        }
        bootJar { enabled = true }
        jar { enabled = true }
    }
}

